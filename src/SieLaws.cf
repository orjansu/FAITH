entrypoints LawList, MetaVar;

DLawList. LawList ::= [Law];

comment "--";
comment "{-" "-}";

DLaw. Law ::= CommandName ":" Term ImpRel Term SideCond;
terminator Law ";";
token CommandName ('-' (letter | '@' ) (letter | '-' | '@' | digit| '\'')*);
-- copied from Sie.cf

DefinedEqual.        ImpRel ::= "=def=";
StrongImprovementLR. ImpRel ::= "|~>";
WeakImprovementLR.   ImpRel ::= "|~~>";
StrongImprovementRL. ImpRel ::= "<~|";
WeakImprovementRL.   ImpRel ::= "<~~|";
StrongCostEquiv.     ImpRel ::= "<~>";
WeakCostEquiv.       ImpRel ::= "<~~>";

token MVLetBindings      ('G' digit*);
token MVValue            ('V');
token MVContext          ('C' | 'D');
token MVIntegerVar       ('v' digit* | 'w' digit* | 'n' | 'm' | 'i' | 'j');
token MVVar              ('x' | 'y' | 'f' | 'z');
token MVVarVect          ('x' 's' | 'y' 's');
token MVValueContext     ('V' 'c' 't' 'x');
token MVReduction        ('R');
token MVVarSet           ('X' | 'Y');
token MVTerm             ('M' | 'N');
token MVTerms            ('N' '_' 'i');
token MVPatterns         ('p' 'a' 't' '_' 'i');
token MVCaseStm          ('a' 'l' 't' 's' digit*);
token MVConstructorName  ('c' digit*);
-- NOTE: This is copied to Sie.cf and both should be equal. I have not yet found
-- a way to share grammar between .cf files.

rules MetaVar ::= MVLetBindings | MVValue | MVContext | MVIntegerVar | MVVar | MVVarVect | MVValueContext | MVReduction | MVVarSet | MVTerm | MVPatterns | MVCaseStm | MVConstructorName;

TValueMetaVar.    Term4 ::= MVValue;
TGeneralMetaVar.  Term4 ::= MVTerm;
TMVTerms.         Term4 ::= MVTerms;
TVar.             Term4 ::= Var;
TAppCtx.          Term4 ::= MVContext "[" Term "]";
TAppValCtx.       Term4 ::= MVValueContext "[" Term "]";
TNonTerminating.  Term4 ::= "@" ;
TNum.             Term4 ::= Integer;
TIndVar.          Term4 ::= Var "^" IndExpr;
TConstructor.     Term4 ::= Constructor;
TStackSpike.      Term4  ::= "s^" Term4;
TStackSpikes.     Term4  ::= "["StackWeight"]s^" Term4;
THeapSpike.       Term4  ::= "h^" Term4;
THeapSpikes.      Term4  ::= "["HeapWeight"]h^" Term4;
TDummyBinds.      Term4  ::= VarSet "d^" Term4;
TRedMetaVar.      Term4 ::= MVReduction "[" Term "]";
TRedMetaVarW.     Term4 ::= RedWeight "^" MVReduction "[" Term "]" ;

TSubstitution.    Term3  ::= Term4 "[" Var "/" Var "]";

TRApp.        Term2 ::= Term2 Var;
TRAppW.       Term2 ::= RedWeight "(" Term2 Var ")";
TRAppNoW.     Term2 ::= Term2 "<>" Var;

TRPlus.    Term1    ::= Term1 "+" Term2;
TRPlusW1.  Term1    ::= RedWeight "(" Term1 "+" Term2 ")";
TRPlusW2.  Term1    ::= Term1 "+" RedWeight Term2;
TRPlusWW.  Term1    ::= RedWeight "(" Term1 "+" RedWeight Term2 ")";

TLam.             Term  ::= "\\" Var "." Term ;
TLet.             Term  ::= "let" LetBindings "in" Term;

TRCase.       Term ::= MaybeRedWeight "case" Term "of" "{" [CaseStm] "}";
TRAddConst.   Term ::= MaybeRedWeight "add" IntExpr Term1;
TRIsZero.     Term ::= MaybeRedWeight "iszero" Term1;
TRSeq.        Term ::= MaybeRedWeight "seq" Term4 Term4;

coercions Term 4;

WithRedWeight. MaybeRedWeight ::= RedWeight;
NoRedWeight.   MaybeRedWeight ::= ;

separator Var "";
VSMetaVar.  VarSet ::= MVVarSet;
VSVectMeta. VarSet ::= "{" MVVarVect "}";
VSConcrete. VarSet ::= "{" [Var] "}"; -- variables are separated by space.
_.          VarSet ::= "{" VarSet "}";
VSFreeVars. VarSet ::= "FV" "(" VarContainer ")";
VSDomain.   VarSet ::= "dom" MVLetBindings;
DVar.    Var ::= MVVar;

VCTerm.           VarContainer ::= Term;
VCMetaVarContext. VarContainer ::= MVContext;
VCMetaVarRed.     VarContainer ::= MVReduction;
VCValueContext.   VarContainer ::= MVValueContext;

CSAlts.     CaseStm ::= MVCaseStm;
CSPatterns. CaseStm ::= MVPatterns "->" Term;
CSConcrete. CaseStm ::= Constructor "->" Term;
separator CaseStm ",";

CGeneral.     Constructor ::= MVConstructorName MVVarVect;
CTrue.        Constructor ::= "true";
CFalse.       Constructor ::= "false";

LBSOnlyMeta. LetBindings ::= [MetaBindSet];
LBSBoth.     LetBindings ::= [MetaBindSet] "{" [LetBinding] "}";

MBSMetaVar.  MetaBindSet ::= MVLetBindings;
MBSSubstitution. MetaBindSet ::= MVLetBindings "[" Var "/" Var "]";
separator MetaBindSet "";

DLetBinding. LetBinding ::= Var BindSymbol Term;
separator LetBinding ",";
BSWeights.   BindSymbol ::= "=[" StackWeight "," HeapWeight "]=";
BSNoWeight.  BindSymbol ::= "=";

DStackWeight. StackWeight ::= IntExpr;
DHeapWeight.  HeapWeight ::= IntExpr;
DIndExpr.     IndExpr ::= IntExpr1;

DIntegerVar. IntegerVar ::= MVIntegerVar;

IEVar.   IntExpr1 ::= IntegerVar;
IENum.   IntExpr1 ::= Integer;
IESizeBind. IntExpr1 ::= "|" MVLetBindings "|";
IEPlus.  IntExpr  ::= IntExpr "+" IntExpr1;
IEMinus. IntExpr  ::= IntExpr "-" IntExpr1;
coercions IntExpr 1;

DRedWeight. RedWeight ::= "[" StackWeight "]";

---- if-statements ---
NoSideCond. SideCond ::= ;
WithSideCond. SideCond ::= "if" BoolTerm;

BTSizeEq.    BoolTerm ::= "|" MVLetBindings "|" "=" "|" MVLetBindings "|";
BTSetEq.     BoolTerm ::= SetTerm "=" SetTerm;
BTSubsetOf.  BoolTerm ::= SetTerm "subsetof" SetTerm;
BTIn.        BoolTerm ::= Var "in" SetTerm;
BTNot.       BoolTerm ::= "not" BoolTerm ;
BTLE.        BoolTerm ::= IntExpr "=<" IntExpr;
BTGE.        BoolTerm ::= IntExpr ">=" IntExpr;
BTGT.        BoolTerm ::= IntExpr ">" IntExpr;
BTIsFresh.   BoolTerm ::= Var "is" "fresh";
BTAreFresh.  BoolTerm ::= MVVarVect "are" "fresh";
BTReducesTo. BoolTerm ::= MVReduction "[" MVValue "]" "~~>" MVTerm;

STMetaBindSet.  SetTerm1 ::= MetaBindSet;
STVarSet.       SetTerm1 ::= VarSet;
STUnion.        SetTerm ::= SetTerm "union" SetTerm1;
STDifference.   SetTerm ::= SetTerm "\" SetTerm1;
STEmpty.        SetTerm ::= ; -- Oklart
coercions SetTerm 1;
